alias userSP R1;
userSP = SP;

[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 13] = SP;
SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11] * 512 - 1;

alias syscall R2;
syscall = [[PTBR + 2 * (userSP - 5) / 512] * 512 + (userSP - 5) % 512];

alias returnAddress R3;
returnAddress = [PTBR + 2 * (userSP - 1) / 512] * 512 + (userSP - 1) % 512;

[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = syscall;

if (syscall == 2) then
        // Open
        alias fileName R4;
        fileName = [[PTBR + 2 * ((userSP - 4) / 512)] * 512 + (userSP - 4) % 512];

        alias UAPN R5;
        UAPN = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11];
        
        alias index R6;
        index = -1;
        
        alias i R7;
        i = 0;
        while (i < 8) do
                if ([UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * i] == -1) then
                        index = i;
                        break;
                endif;
                i = i + 1;
        endwhile;

        if (index == -1) then
                [returnAddress] = -3;
        else
                multipush(R1,R2,R3,R4,R5,R6,R7);
                R1 = 3;
                R2 = fileName;
                call MOD_3;
                multipop(R1,R2,R3,R4,R5,R6,R7);

                alias fileIndex R0;

                if (fileIndex == -1 || fileIndex == -2) then
                        [returnAddress] = fileIndex;
                else
                        [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index + 0] = FILE;
                        [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index + 1] = fileIndex;
                        [returnAddress] = index;
                endif;
        endif;
endif;

if (syscall == 3) then
        // Close
        alias index R4;
        index = [[PTBR + 2 * ((userSP - 4) / 512)] * 512 + (userSP - 4) % 512];

        alias UAPN R5;
        UAPN = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11];

        if (index < 0 || index > 7 || [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index] != FILE) then
                [returnAddress] = -1;
        else
                alias fileIndex R6;
                fileIndex = [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index + 1];

                multipush(R1,R2,R3,R4,R5,R6);
                R1 = 4;
                R2 = fileIndex;
                call MOD_3;
                multipop(R1,R2,R3,R4,R5,R6);

                [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index + 0] = -1;
                [UAPN * 512 + RESOURCE_TABLE_OFFSET + 2 * index + 1] = -1;

                [returnAddress] = 0;
        endif;
endif;

SP = userSP;
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
ireturn;
